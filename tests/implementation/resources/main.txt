*** Settings ***
Documentation     Main resource file for EdenTests
Resource  auth.txt
Resource  base.txt
Resource  crud.txt
Resource  filter.txt
Resource  datatable.txt
Resource  widgets.txt
Resource  form.txt
Library  Selenium2Library  run_on_failure=Catch Error If Available Else Take Screenshot
Library  Collections
Variables  ../../execution/settings/config.py
Library  ../../execution/libs/edentest_robot.py  ${SERVER}  ${APPNAME}  ${ADMIN EMAIL}  ${ADMIN PASSWORD}
Library  ../../execution/libs/edentest_database_local.py

*** Variables ***
${BASEURL}  ${SERVER}/${APPNAME}
${HOMEPAGE}  http://${BASEURL}/default/index

*** Keywords ***
Start Browser With Proxy
    ${proxy}=  Evaluate  sys.modules['selenium.webdriver'].Proxy()  sys, selenium.webdriver
    ${proxy.http_proxy}=  Set Variable  ${HTTP_PROXY}
    ${proxy.no_proxy}=  Set Variable  ${NO_PROXY}
    Create Webdriver  ${BROWSER}  proxy=${proxy}

Start Browser
    Create Webdriver  ${BROWSER}

Start Testing
    [Documentation]  Starts the test, sets the template global variable
    ${deployment settings} =  Get Deployment Settings  template  database_type  base_allow_repeatability
    ${TEMPLATE}=  Get From Dictionary  ${deployment settings}  template
    ${DB TYPE}=  Get From Dictionary  ${deployment settings}  database_type
    ${REPEATABILITY}=  Get From Dictionary  ${deployment settings}  base_allow_repeatability
    Switch To Temp Database  ${DB TYPE}  ${REPEATABILITY}
    Set Global Variable  ${REPEATABILITY}
    Set Global Variable  ${DB TYPE}
    Set Global Variable  ${TEMPLATE}
    Run Keyword If  "${HTTP_PROXY}" != "" and "${BROWSER}" == "Firefox"  Start Browser With Proxy
    ...  ELSE  Start Browser
    Set Selenium Speed  ${DELAY}

End Testing
    [Documentation]  Ends the test
    Switch To Original Database  ${DB TYPE}  ${REPEATABILITY}
    Logout From Eden
    Close Browser
